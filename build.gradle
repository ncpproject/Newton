plugins {
    id 'application'
    id 'org.beryx.jlink' version '2.17.3'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'maven'
}

group 'com.Newton'
version '0.0.1'

sourceCompatibility = 14
targetCompatibility = 14

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

mainClassName = 'Newton/com.Newton.Newton.Main'

javafx {
   // sdk = 'J:/Apps/JAVA/JavaFX Windows SDK/javafx-sdk-13.0.2'
    version = "14"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.graphics']
}

compileJava {
    options.encoding = "UTF-8"
}

compileTestJava {
    options.encoding = "UTF-8"
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.12.1'
    implementation 'com.electronwill.night-config:toml:3.6.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:11.3.4'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign-pack:11.3.4'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'io.reactivex.rxjava2:rxjavafx:2.2.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.nayuki:qrcodegen:1.6.0'
    implementation 'com.github.wavesfx:WavesJ:master-SNAPSHOT'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.10.0'

}

//applicationDefaultJvmArgs = ['--add-exports=javafx.base/com.sun.javafx.event=org.controlsfx.controls']
applicationDefaultJvmArgs = ['-splash:./package/Newton_splash.png']

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

ext.os = org.gradle.internal.os.OperatingSystem.current()

jlink {
    launcher {
        name = 'Newton'
        jvmArgs = ['-Djdk.tls.client.protocols=TLSv1.2', "-splash:{{BIN_DIR}}//Newton_splash.png"]
    }
    forceMerge('log4j-api')
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")

    jpackage {
        skipInstaller = false
        imageName = "Newton"
        installerName = 'Newton'
        appVersion = version
        installerOptions = [
                '--vendor', 'Newton Wallet',
                '--license-file', 'LICENSE'
        ]
        if (os.windows) {
            imageOptions = ['--icon', './package/Newton_icon.ico']
            jvmArgs = ['-Djdk.tls.client.protocols=TLSv1.2', '-splash:$ROOTDIR/runtime/bin/Newton_splash.png']
            installerType = "msi"
            installerOptions += ['--win-upgrade-uuid', 'd4a56a68-bdc2-4138-8572-84fc72c11fbc',
                                '--win-dir-chooser',
                                '--win-menu',
                                '--win-shortcut',
            ]
        } else if(os.macOsX) {
            imageOptions = ['--icon', './package/Newton_mac_icon.icns',]
            jvmArgs = ['-Djdk.tls.client.protocols=TLSv1.2', '-splash:$ROOTDIR/runtime/Contents/Home/bin/Newton_splash.png']
            installerType = 'dmg'
        } else if (os.unix) {
            imageOptions = ['--icon', './package/Newton_linux_icon.png']
            jvmArgs = ['-Djdk.tls.client.protocols=TLSv1.2', '-splash:$ROOTDIR/lib/runtime/bin/Newton_splash.png']
            installerType = "deb"
            installerOptions += [
                    '--linux-shortcut'
            ]
        }
    }
}

def JLINK_DIR = "$buildDir"
tasks.jlink.doLast {
    copy {
        includeEmptyDirs = false
        from("package") {
            include "Newton_splash.png"
        }
        into JLINK_DIR + "/image/bin"
    }
}